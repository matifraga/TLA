package Raptor;

import java_cup.runtime.*;
import java.io.PrintWriter;
import java.util.HashMap;
import java.util.Map;

parser code {:

    public enum VariableType { INTEGER, STRING;}

    private static PrintWriter writer = null;
    private static Map<String, VariableType> variables;

	public static void main(String args[]) throws Exception {
		SymbolFactory sf = new DefaultSymbolFactory();
		if (args.length==0) new Parser(new Scanner(System.in,sf),sf).parse();
		else {
		    String fileName = args[0].split("\\.")[0];
		    writer = new PrintWriter(fileName + ".java", "UTF-8");
		    writeToFile("public class " + fileName +" {\n");
		    new Parser(new Scanner(new java.io.FileInputStream(args[0]),sf),sf).parse();
		}
	}
	
	public static void writeToFile(String s) {
	    writer.print(s);
	}
	
	public static void closeWriter() {
	    writeToFile("}");
	    writer.close();
	}
	
	public static void registerVariable(String name, VariableType type) {
	    variables.put(name, type);
	}
	
	public static boolean variableExists(String name) {
	    return variables.containsKey(name);
	}
	
	public static VariableType getType(String name) {
	    return variables.get(name);
	}
	
:}

terminal PLUS, TIMES, MINUS, DIVIDE, ASSIGN, MOD, LPAREN, RPAREN, JUMP, BEGIN, FINISH, AS_PLUS, AS_MINUS, AS_TIMES, AS_DIV, IO_OUT, IO_IN, IF, ELSE, LOOP, END, CMP_LT, CMP_GT, CMP_EQ, CMP_NE, BOOL_OR, BOOL_AND, BOOL_NOT, TRUE, FALSE;
terminal String  NUM_DEF, TEXT, VAR_NAME, TEXT_DEF, CONST_DEF;
terminal Integer NUMBER;

non terminal s;
non terminal String codee, code_line, var, text_expr, if_block, boolean_exp, boolean_term;
non terminal Integer num_expr;

precedence left PLUS;
precedence left MINUS;
precedence left TIMES;
precedence left DIVIDE;
precedence left MOD;

s               ::= BEGIN JUMP codee:e FINISH 
                    {: 
                       parser.writeToFile("public static void main(String args[]) { \n");
                       parser.writeToFile(e);
                       parser.writeToFile("}\n");
                       parser.closeWriter(); 
                    :}
                ;
                    
codee           ::= codee:a code_line:e JUMP 
                    {: RESULT= a + e + ";\n"; :}
                |   code_line:e JUMP
                    {: RESULT= e + ";\n"; :}
                ;
                
code_line       ::= num_expr:e 
                    {: RESULT=String.valueOf(e); :}
                |   var:e
                    {: RESULT=e; :}
                |   IO_OUT LPAREN text_expr:e RPAREN
                    {: RESULT= "System.out.print(" + e + ")"; :}
                |   IO_OUT LPAREN num_expr:e RPAREN
                    {: RESULT= "System.out.print(" + e + ")"; :}
                |   if_block:e
                    {: RESULT=e ;:}
                |   LOOP boolean_exp:d JUMP codee:e END
                    {: RESULT="while(" + d + "){\n" + e + "}"; :}
                ;
                
if_block        ::= IF boolean_exp:b JUMP codee:e ELSE JUMP codee:j END
                    {: RESULT= "if (" + b + ") {\n" + e + "} else {\n" + j + "}"; :}
                |   IF boolean_exp:b JUMP codee:e END
                    {: RESULT= "if (" + b + ") {\n" + e + "}" ; :}
                ;
                
boolean_exp     ::= LPAREN boolean_exp:a RPAREN
                    {: RESULT= "(" + a + ")";:}
                |   boolean_term:s BOOL_OR boolean_exp:q
                    {: RESULT= s + " || " + q ;:}
                |   boolean_term:s BOOL_AND boolean_exp:t
                    {: RESULT= s + " && " + t ;:}
                |   boolean_term:h
                    {: RESULT= h ; :}
                |   BOOL_NOT boolean_exp:g
                    {: RESULT= "!" + g ;:} 
                ;
                
boolean_term    ::= TRUE
                    {: RESULT= "true" ;:}
                |   FALSE
                    {: RESULT= "false" ;:}
                |   LPAREN num_expr:a CMP_LT num_expr:t RPAREN
                    {: RESULT= "(" + a + " < " + t + ")" ;:}
                |   LPAREN num_expr:a CMP_GT num_expr:t RPAREN
                    {: RESULT= "(" + a + " > " + t + ")" ;:}
                |   LPAREN num_expr:a CMP_EQ num_expr:t RPAREN
                    {: RESULT= "(" + a + ".equals(" + t + "))" ;:}
                |   LPAREN num_expr:a CMP_NE num_expr:t RPAREN
                    {: RESULT= "(" + a + " != " + t + ")" ;:}
                ;
                 
var             ::= NUM_DEF:e
                    {: RESULT="Integer " + e ; :}
                |   NUM_DEF:e ASSIGN num_expr:a
                    {: RESULT="Integer " + e + " = " + a ; :}
                |   TEXT_DEF:e
                    {: RESULT="String " + e ; :}
                |   TEXT_DEF:e ASSIGN text_expr:a
                    {: RESULT="String " + e + " = " + a ; :}
                |   VAR_NAME:a AS_PLUS text_expr:e
                    {: RESULT= a + " = " + a + ".concat(" + e + ")"; :}
                |   VAR_NAME:a AS_PLUS num_expr:e
                    {: RESULT= a + " = " + a + " + " + e; :}
                |   VAR_NAME:a AS_MINUS num_expr:e
                    {: RESULT= a + " = " + a + " - " + e; :}
                |   VAR_NAME:a AS_TIMES num_expr:e
                    {: RESULT= a + " = " + a + " * " + e; :}
                |   VAR_NAME:a AS_DIV num_expr:e
                    {: RESULT= a + " = " + a + " / " + e; :}
                |   CONST_DEF:a ASSIGN num_expr:e
                    {: RESULT= "final Integer " + a + " = " + e ;:}
                |   CONST_DEF:a ASSIGN text_expr:e
                    {: RESULT= "final String " + a + " = " + e ;:}
                ;
                
text_expr       ::= TEXT:e
                    {: RESULT=e; :}
                |   text_expr:a PLUS text_expr:r
                    {: RESULT= a + r ; :}
                ;
                
num_expr        ::= NUMBER:n  
                    {: RESULT=n; :}
                | num_expr:l PLUS num_expr:r
	                {: RESULT=new Integer(l.intValue() + r.intValue()); :}
                | num_expr:l MINUS num_expr:r
	                {: RESULT=new Integer(l.intValue() - r.intValue()); :}
	            | num_expr:l TIMES num_expr:r
	                {: RESULT=new Integer(l.intValue() * r.intValue()); :}
	            | num_expr:l DIVIDE num_expr:r
	                {: RESULT=new Integer(l.intValue() / r.intValue()); :}
	            | num_expr:l MOD num_expr:r
	                {: RESULT=new Integer(l.intValue() % r.intValue()); :}
	            | LPAREN num_expr:e RPAREN
	                {: RESULT=e; :}
	            ;
